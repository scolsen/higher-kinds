;; Copyright 2020 Google LLC
;;
;; Licensed under the Apache License, Version 2.0 (the "License");
;; you may not use this file except in compliance with the License.
;; You may obtain a copy of the License at
;;
;;  https://www.apache.org/licenses/LICENSE-2.0
;;
;;  Unless required by applicable law or agreed to in writing, software
;;  distributed under the License is distributed on an "AS IS" BASIS,
;;  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
;;  See the License for the specific language governing permissions and
;;  limitations under the License.

;; The fmap interface, defined against the type of type constructors (higher-kinds)
(definterface fmap (Fn [(Ref (Fn [a] b)) (f a)] (f b)))
(definterface fmap! (Fn [(Ref (Fn [a] b)) (Ref (f a))] Unit))

;; Instances must implement fmap at minimum.
(defmodule Functor
  (doc functor?
    "Returns true if a given kind is a Functor instance."
    ""
    "In other words, returns true if the kind implements fmap.")
  (defndynamic functor? [k]
    ;; We cannot call defined directly, otherwise it evaluates
    ;; against "(Symbol.prefix k)" instead of the result of that computation
    (list 'defined? (Symbol.prefix k 'map)))

 (doc replace
   "Replaces the inhabitants of a functor with a constant value `x`."
   ""
   "Analogous to <$ in Haskell.")
 (sig replace (Fn [b (f a)] (f b)))
 (defn replace [x functor]
   (fmap &(const x) functor))

 (doc replace!
   "Replaces the inhabitants of a functor with the value `x` in place.")
 (sig replace! (Fn [b (Ref (f a))] Unit))
 (defn replace! [x functor-ref]
   (fmap! &(const x) functor-ref))

 (doc void
   "Replaces the inhabitant of a functor with Unit.")
 (sig void (Fn [(f a)] (f Unit)))
 (defn void [functor]
   (fmap &(fn [x] ()) functor))

 (doc void
   "Replaces the inhabitant of a functor with Unit in place.")
 (sig void! (Fn [(Ref (f a))] Unit))
 (defn void! [functor-ref]
   (fmap! &(fn [x] ()) functor-ref))
)

